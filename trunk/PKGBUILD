# Maintainer: David Runge <dvzrv@archlinux.org>

pkgname=giada
pkgver=0.17.2
pkgrel=1
pkgdesc="A free, minimal, hardcore audio tool for DJs, live performers and electronic musicians"
arch=('x86_64')
url="https://www.giadamusic.com/"
license=('GPL3')
groups=('pro-audio')
depends=('gcc-libs' 'glibc' 'libx11' 'libxpm')
# upstream vendors a hacked rtaudio :(
makedepends=('alsa-lib' 'catch2' 'cmake' 'fltk' 'gendesk' 'imagemagick' 'jack'
'libpulse' 'libsamplerate' 'libsndfile' 'libxrandr' 'nlohmann-json' 'rtmidi')
checkdepends=('xorg-server-xvfb')
provides=('vst3-host')
source=("https://www.giadamusic.com/data/${pkgname}-${pkgver}-src.tar.gz"
        "$pkgname-0.17.2-devendor_nlohmann_json.patch"
        "$pkgname-0.17.2-executable_install.patch::https://github.com/monocasual/giada/pull/462/commits/cb2d5e18befa79905acd31bf2fafc610f22c79fa.patch"
        "$pkgname-0.17.2-missing_string_include.patch::https://github.com/monocasual/giada/pull/461/commits/5932bc51d5bdca02662ee99ecf8f95b53b4e656e.patch")
sha512sums=('7f59593b3b98a3d012a769e5293727ef4444c9a71185855f4f832e9860b93f502d4b8476a6f69c64bbec80ccf3c4b5f665db4b05329ee6ce837cebfaafa0cac4'
            'b9d952c0816dd2b81dadff24521b19f305ca5ddeb1964dd81ce93e6f63b25dbf291c07e528cc2836f268a095bf9920786331a615230416f44a9014ff581a0bce'
            'd2581abb178661953e7661a12ab011b7f145d9c43fb3ef75d0a057a6ee2a11b19152980a8e0817e5ef96b026e6f318913374f5573b7b63606bce1818bc808978'
            'df2b6b7d868d7f738c1ce6532f8f5b7cdcc867a5c7bd560fe8de38675619679cf4e8d330ffda61b3203e7836a8610a9518af3079f8ff322f3c82f3a361189d57')
b2sums=('b65b3502eaca99fd33919096591ccb5d3bb74432977ecae27022f11536b0c6ed2fc2dc014f2951d90c3bbf9893af2088945ed9df8c23ad9866e89f25dd268a7c'
        'a482f356969c5d184a1eb0945477040cc7c5d1447fee0159cc5d2da05faea0bd696123b0559bdced148b301181f93726a515df24a814dbd08d207e2959f31305'
        'bd3b813d262eacc530cb633fa143e794d5e558da00d25bd70a2e32128711111f44a8bf207084d31b8fe32b8114dca8b2ec77c56fd2ce60ee923031632fa83f20'
        '6c4b796d167678783377e5e46d19d24f055b6cc352f8c87ab5bd27f5f5b0e7624926f294d49633c8086ae1e5c6da8b994700eb7d298b02989df51feba557b920')

prepare() {
  mv -v "$pkgname-$pkgver-src" "$pkgname-$pkgver"
  cd "$pkgname-$pkgver"
  # XDG desktop file
  gendesk -n \
          --pkgname ${pkgname} \
          --pkgdesc "${pkgdesc}" \
          --name Giada \
          --categories "AudioVideo;Audio;Midi;Sequencer"

  # devendor nlohmann-json
  patch -Np1 -i ../"$pkgname-0.17.2-devendor_nlohmann_json.patch"
  # install the executable to the bin directory
  patch -Np1 -i ../"$pkgname-0.17.2-executable_install.patch"
  # fix missing string include
  patch -Np1 -i ../"$pkgname-0.17.2-missing_string_include.patch"
}

build() {
  cd "$pkgname-$pkgver"
  cmake -DCMAKE_INSTALL_PREFIX=/usr \
        -DCMAKE_BUILD_TYPE='None' \
        -DWITH_VST3=ON \
        -DWITH_TESTS=ON \
        -W no-dev \
        -B build \
        -S .
  make VERBOSE=1 -C build
}

# # unclear how to run tests: https://github.com/monocasual/giada/issues/463
# check(){
#   cd "$pkgname-$pkgver"
#   make check VERBOSE=1 -C build
# }

package() {
  depends+=('libasound.so' 'libfltk.so' 'libfreetype.so' 'libjack.so'
  'libpulse.so' 'libpulse-simple.so' 'librtmidi.so' 'libsamplerate.so'
  'libsndfile.so')
  cd "$pkgname-$pkgver"
  make DESTDIR="$pkgdir/" install -C build
  # XDG integration
  install -vDm 644 "${pkgname}.desktop" -t "${pkgdir}/usr/share/applications"
  install -vDm 644 "extras/${pkgname}-logo.svg" \
    "${pkgdir}/usr/share/icons/hicolor/scalable/apps/${pkgname}.svg"
  # docs
  install -vDm 644 {ChangeLog,README.md} \
    -t "${pkgdir}/usr/share/doc/${pkgname}"
}
