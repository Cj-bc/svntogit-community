# Maintainer: Daniel M. Capella <polyzen@archlinux.org>

pkgname=firefox-stylus
pkgver=1.5.16
pkgrel=1
pkgdesc='Easily install themes and skins for many popular sites'
url=https://add0n.com/stylus.html
arch=('any')
license=('GPL3')
groups=('firefox-addons')
makedepends=('git' 'npm')
source=("https://github.com/openstyles/stylus/archive/v$pkgver/stylus-v$pkgver.tar.gz")
b2sums=('8dcf4a06fca14e2011e86247f4287b0e58f26a3963bcd302795f597643df0858d1ac08c23d10af4de5037fac9da880a087cc9815dab0b1cae89170a1ed9e955b')

prepare() {
  git config --global url."https://github.com".insteadOf ssh://git@github.com
}

build() {
  cd stylus-$pkgver
  npm ci
  npm run zip
}

package() {
  cd stylus-$pkgver
  install -Dm644 stylus.zip \
    "$pkgdir"/usr/lib/firefox/browser/extensions/{7a7a4a92-a2a0-41d1-9fd7-1e92480d612d}.xpi
}

# vim:set ts=2 sw=2 et:
