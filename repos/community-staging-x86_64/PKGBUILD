# Maintainer: Antonio Rojas <arojas@archlinux.org>
# Contributor: RÃ©my Oudompheng <oudomphe@clipper.ens.fr>

pkgname=pynac
pkgver=0.7.27
pkgrel=2
pkgdesc='Python version of GiNaC, a C++ library for symbolic mathematical calculations'
arch=(x86_64)
url='http://pynac.org'
license=(GPL)
depends=(python singular) # giac disabled because of https://trac.sagemath.org/ticket/26427
source=(https://github.com/pynac/pynac/archive/$pkgname-$pkgver.tar.gz
        https://github.com/pynac/pynac/commit/850ee9ea.patch)
sha256sums=('125414a00237eb80d26628875b4e9bdcbfa3ba76e178b2e6e97532515cbcc4e0'
            'ecd4bc18fecde2d073be0307b2936586eacd600cb54cc63793ea0aed1cd31a7f')

prepare() {
  cd pynac-pynac-$pkgver
  ./bootstrap

  patch -p1 < ../850ee9ea.patch # Fix build with GCC 11
}

build() {
  cd pynac-pynac-$pkgver

  ./configure --prefix=/usr
  #https://bugzilla.gnome.org/show_bug.cgi?id=655517
  sed -i -e 's/ -shared / -Wl,-O1,--as-needed\0/g' libtool
  make
}

package() {
  cd pynac-pynac-$pkgver
  make DESTDIR="$pkgdir" install
}
