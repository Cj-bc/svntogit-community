# Maintainer: David Runge <dvzrv@archlinux.org>

pkgname=giada
pkgver=0.19.0
pkgrel=2
pkgdesc="A free, minimal, hardcore audio tool for DJs, live performers and electronic musicians"
arch=(x86_64)
url="https://www.giadamusic.com/"
license=(GPL3)
groups=(pro-audio)
depends=(gcc-libs glibc hicolor-icon-theme libx11 libxcursor libxft
libxinerama libxpm)
# upstream vendors a hacked rtaudio :(
makedepends=(alsa-lib catch2 cmake fltk imagemagick jack libpulse
libsamplerate libsndfile libxrandr nlohmann-json rtmidi)
checkdepends=(xorg-server-xvfb)
provides=(vst3-host)
source=(
  "https://www.giadamusic.com/data/${pkgname}-v${pkgver}-src.tar.gz"
  "${pkgname}-0.19.0-xdg_icon.patch::https://github.com/monocasual/giada/pull/524/commits/e3e55b9d041cec5938edfae73d627aa5b458633d.patch"
  "$pkgname-0.19.0-devendor_nlohmann_json.patch"
)
sha512sums=('da5e4f319a36db316fbbeb66e2b4153412c73a6bc62672df1c3405aaa6ea11867825e0bf2ec60b2f5b51e5c75630b4bf1f74ecf5228b847c832fdfca5c35dca7'
            '0ff136460d52a8561b665fc3beebbaa44914b4f0493d6847971093ac40d98fad2b25b23189a7ab023d9542f64691d9195956dbe68b0dadc3f9d77381769c4ef1'
            '4a6be28c0ba1656e4ce63504c031d6a89ae5fdf10a5652021e5a248e3202526095d097cf1337adf03efa7f618a485aadeb5b6024f15950a7b72cd23ad8cd97d6')
b2sums=('3ad01405dc02d137ff15043d0e89b75a585c925ed830a797f065d572e0a11379856542c4292fa52640208d8d0e2dbee7ccb42a0910ef70c5bb641f4a80126fcf'
        '5e8ba847bda8734b11a37b3179eaf512a8bf2956fa0cba01ad2113575a9f7d79eb0863e550da0a80a2c81ecb06762b0d11ced379fda35d60e2cc44ed2514a159'
        '41fce95d1df10e78c080553184bb078abe30eec7ca85b9d3883ffd2191e4b51decee9c946f0b1aa10852e054c28a4e5f8bde62d4f0093d95e6197df464f61e59')

prepare() {
  mv -v "$pkgname-$pkgver-src" "$pkgname-$pkgver"
  cd "$pkgname-$pkgver"
  # devendor nlohmann-json
  patch -Np1 -i ../"$pkgname-0.19.0-devendor_nlohmann_json.patch"
  # fix XDG icon name: https://github.com/monocasual/giada/pull/524
  patch -Np1 -i ../"${pkgname}-0.19.0-xdg_icon.patch"
}

build() {
  cd "$pkgname-$pkgver"
  cmake -DCMAKE_INSTALL_PREFIX=/usr \
        -DCMAKE_BUILD_TYPE='None' \
        -DWITH_VST3=ON \
        -W no-dev \
        -B build \
        -S .
  cmake -DCMAKE_INSTALL_PREFIX=/usr \
        -DCMAKE_BUILD_TYPE='None' \
        -DWITH_VST3=ON \
        -DWITH_TESTS=ON \
        -W no-dev \
        -B build-test \
        -S .
  make VERBOSE=1 -C build
  make VERBOSE=1 -C build-test
}

check(){
  cd "$pkgname-$pkgver"
  xvfb-run ./build-test/giada --run-tests
}

package() {
  depends+=(libasound.so libfreetype.so libjack.so libpulse.so
  libpulse-simple.so librtmidi.so libsamplerate.so libsndfile.so)

  cd "$pkgname-$pkgver"
  make DESTDIR="$pkgdir/" install -C build
  # docs
  install -vDm 644 {ChangeLog,README.md} \
    -t "${pkgdir}/usr/share/doc/${pkgname}"
}
