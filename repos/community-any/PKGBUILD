# Maintainer: Eli Schwartz <eschwartz@archlinux.org>

_pkgname=FanFicFare
pkgname=fanficfare
pkgver=3.27.0
pkgrel=1
pkgdesc="A tool for downloading fanfiction to eBook formats"
arch=('any')
url="https://github.com/JimmXinu/${_pkgname}"
license=('Apache')
changelog=changelog.txt
depends=('python' 'python-beautifulsoup4' 'python-chardet' 'python-cloudscraper' 'python-html5lib'
         'python-html2text')
optdepends=('python-pillow: support for converting/resizing story images and covers')
source=("${pkgname}-${pkgver}.tar.gz::${url}/archive/v${pkgver}.tar.gz")
sha256sums=('5ea3754fb2db5c19daf7cd6356f5b76136717717517cedb943bb08ef8ba6b8e5')
b2sums=('5599b1071402b452e6643f4c0268e5ab302d7c9eab4b6cc6be31c12684f73f539c02b51e27f1679714b2abb94b8e1ab6d25cac6b28ab926919a417bc97bbbe4e')

build() {
    cd "${srcdir}/${_pkgname}-${pkgver}"

    python setup.py build
}

package() {
    cd "${srcdir}/${_pkgname}-${pkgver}"

    python setup.py install --root="${pkgdir}" --optimize=1 --skip-build
}
