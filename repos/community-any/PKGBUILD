# Maintainer: Eli Schwartz <eschwartz@archlinux.org>

_pkgname=cloudscraper
pkgname=python-cloudscraper
pkgver=1.2.52
pkgrel=1
pkgdesc="Python module to bypass Cloudflare's anti-bot page"
arch=('any')
url="https://github.com/VeNoMouS/${_pkgname}"
license=('MIT')
depends=('python-pyparsing' 'python-requests' 'python-requests-toolbelt')
optdepends=('python-js2py: alternative interpreter/solver'
            'nodejs: alternative interpreter/solver')
makedepends=('python-setuptools')
checkdepends=('python-pytest' 'python-responses' 'python-js2py' 'nodejs')
source=("${pkgname}-${pkgver}.tar.gz::${url}/archive/${pkgver}.tar.gz")
sha256sums=('0c4e692933b2374fa56b59deea21098bb4f9ca7f82c7640feeba1150704cd00d')
b2sums=('9950a1af5964574667f84db8cfd475a46479463c95f237171f2dc9af8f1c904d886c9e3b3528a56e4c87c7006e7bfc08aa00a5ee2b531acf35fc848f457acf0d')

build(){
    cd "${srcdir}"/${_pkgname}-${pkgver}

    python setup.py build
}

check() {
    cd "${srcdir}"/${_pkgname}-${pkgver}

    python -m pytest
}

package() {
    cd "${srcdir}"/${_pkgname}-${pkgver}

    python setup.py install --root="${pkgdir}" --optimize=1 --skip-build
    install -Dm644 LICENSE "${pkgdir}"/usr/share/licenses/${pkgname}/LICENSE
}
