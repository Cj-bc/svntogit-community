# Maintainer: Antonio Rojas <arojas@archlinux.org>
# Contributor: RÃ©my Oudompheng <oudomphe@clipper.ens.fr>

pkgname=pynac
pkgver=0.7.28
pkgrel=2
pkgdesc='Python version of GiNaC, a C++ library for symbolic mathematical calculations'
arch=(x86_64)
url='http://pynac.org'
license=(GPL)
depends=(python singular) # giac disabled because of https://trac.sagemath.org/ticket/26427
source=(https://github.com/pynac/pynac/archive/$pkgname-$pkgver.tar.gz)
sha256sums=('3652dd8ff068307c90c75a0002d20467c21204cf8f9d9f80eb71a98d41a8e29e')

prepare() {
  cd pynac-pynac-$pkgver
  ./bootstrap
}

build() {
  cd pynac-pynac-$pkgver

  ./configure --prefix=/usr
  #https://bugzilla.gnome.org/show_bug.cgi?id=655517
  sed -i -e 's/ -shared / -Wl,-O1,--as-needed\0/g' libtool
  make
}

package() {
  cd pynac-pynac-$pkgver
  make DESTDIR="$pkgdir" install
}
