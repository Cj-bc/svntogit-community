# Maintainer: Christian Hesse <mail@eworm.de>
# Contributor: Robert Knauer <robert@privatdemail.net>

pkgname=python-pyodbc
_srcname=pyodbc
pkgver=4.0.31
pkgrel=1
pkgdesc='ODBC module for Python'
arch=('x86_64')
url='https://github.com/mkleehammer/pyodbc'
license=('MIT')
depends=('python' 'unixodbc')
source=(https://github.com/mkleehammer/pyodbc/archive/$pkgver/$pkgname-$pkgver.tar.gz)
sha256sums=('3ea585444c13fdf7e9d50651deff1be5d37fdb9d46a6c885e798baf0ee0165dc')

prepare() {
  echo "Version: ${pkgver}.0" > ${_srcname}-${pkgver}/PKG-INFO
}

build() {
  cd ${_srcname}-${pkgver}

  python setup.py build
}

package() {
  cd ${_srcname}-${pkgver}

  python setup.py install --root="${pkgdir}/" --optimize=1
  install -Dm644 LICENSE.txt "${pkgdir}"/usr/share/licenses/${pkgname}/LICENSE.txt
  rm "$pkgdir"/usr/pyodbc.pyi
}
